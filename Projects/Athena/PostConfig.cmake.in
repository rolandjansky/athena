#
# File taking care of pointing the downstream projects at the right
# version of the externals.
#

# Set the versions of the TDAQ projects:
set( TDAQ_VERSION "@TDAQ_VERSION@" )
set( TDAQ_ROOT "@TDAQ_ROOT@" )

set( TDAQ-COMMON_VERSION "@TDAQ-COMMON_VERSION@" )
set( TDAQ-COMMON_ROOT "@TDAQ-COMMON_ROOT@" )

set( DQM-COMMON_VERSION "@DQM-COMMON_VERSION@" )
set( DQM-COMMON_ROOT "@DQM-COMMON_ROOT@" )

# Find Gaudi:
find_package( Gaudi REQUIRED )

# Load all the files from the externals/ subdirectory:
get_filename_component( _thisdir ${CMAKE_CURRENT_LIST_FILE} PATH )
file( GLOB _externals "${_thisdir}/externals/*.cmake" )
unset( _thisdir )
foreach( _external ${_externals} )
   include( ${_external} )
   get_filename_component( _extName ${_external} NAME_WE )
   string( TOUPPER ${_extName} _extNameUpper )
   if( NOT AtlasExternals_FIND_QUIETLY )
      message( STATUS "Taking ${_extName} from: ${${_extNameUpper}_ROOT}" )
   endif()
   unset( _extName )
   unset( _extNameUpper )
endforeach()
unset( _external )
unset( _externals )
