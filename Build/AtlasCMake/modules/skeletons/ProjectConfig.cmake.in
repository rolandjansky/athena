# $Id: ProjectConfig.cmake.in 739623 2016-04-12 10:56:29Z krasznaa $
#
# Configuration file for a RootCore CMake release installation.
# It defines the following variables:
#
#  @CMAKE_PROJECT_NAME@_ROOT_DIR     - Root directory for the release
#  @CMAKE_PROJECT_NAME@_INCLUDE_DIR  - Include directory for the release
#  @CMAKE_PROJECT_NAME@_INCLUDE_DIRS - Include directory for the release
#  @CMAKE_PROJECT_NAME@_LIBRARY_DIR  - Library directory for the release
#  @CMAKE_PROJECT_NAME@_LIBRARY_DIRS - Library directory for the release
#  @CMAKE_PROJECT_NAME@_RUNTIME_DIR  - Runtime directory for the release
#  @CMAKE_PROJECT_NAME@_DEFINITIONS  - Compile definitions needed for the release
#  @CMAKE_PROJECT_NAME@_MODULE_DIR   - Directory holding CMake modules
#  @CMAKE_PROJECT_NAME@_PYTHON_DIR   - Directory holding python code
#
# Note however that most of the time you should not be using any of these
# variables directly, but should rather use the ROOTCORE_PROJECT(...) function
# to set up a work area that compiles against this release.
#

# Get some path names:
get_filename_component( _thisdir@CMAKE_PROJECT_NAME@
   "${CMAKE_CURRENT_LIST_FILE}" PATH )
get_filename_component( _releasedir@CMAKE_PROJECT_NAME@
   "${_thisdir@CMAKE_PROJECT_NAME@}" PATH )

# Set the root directory variable:
get_filename_component( @CMAKE_PROJECT_NAME@_ROOT_DIR
   "${_releasedir@CMAKE_PROJECT_NAME@}" ABSOLUTE )

# Print what project/release was found just now:
if( NOT @CMAKE_PROJECT_NAME@_FIND_QUIETLY )
   message( STATUS "Found @CMAKE_PROJECT_NAME@: "
      "${@CMAKE_PROJECT_NAME@_ROOT_DIR} (version: @CMAKE_PROJECT_VERSION@)" )
endif()

# Set the include directory variable:
get_filename_component( @CMAKE_PROJECT_NAME@_INCLUDE_DIR
   "${_releasedir@CMAKE_PROJECT_NAME@}/include" ABSOLUTE )
set( @CMAKE_PROJECT_NAME@_INCLUDE_DIRS ${@CMAKE_PROJECT_NAME@_INCLUDE_DIR} )

# Set the library directory variable:
get_filename_component( @CMAKE_PROJECT_NAME@_LIBRARY_DIR
   "${_releasedir@CMAKE_PROJECT_NAME@}/lib" ABSOLUTE )
set( @CMAKE_PROJECT_NAME@_LIBRARY_DIRS ${@CMAKE_PROJECT_NAME@_LIBRARY_DIR} )

# Set the runtime directory variable:
get_filename_component( @CMAKE_PROJECT_NAME@_RUNTIME_DIR
   "${_releasedir@CMAKE_PROJECT_NAME@}/bin" ABSOLUTE )
set( @CMAKE_PROJECT_NAME@_BINARY_PATH ${@CMAKE_PROJECT_NAME@_RUNTIME_DIR} )

# Set the python directory variable:
get_filename_component( @CMAKE_PROJECT_NAME@_PYTHON_DIR
   "${_releasedir@CMAKE_PROJECT_NAME@}/python" ABSOLUTE )
set( @CMAKE_PROJECT_NAME@_PYTHON_PATH ${@CMAKE_PROJECT_NAME@_PYTHON_DIR} )

# Set the compiler definitions variable:
set( @CMAKE_PROJECT_NAME@_DEFINITIONS "@ATLAS_DEFINITIONS@" )

# The base projects that this project was built on top of:
set( @CMAKE_PROJECT_NAME@_BASE_PROJECTS @ATLAS_BASE_PROJECTS@ )

# The platform name used for the build. It is overridden in release builds,
# but can be useful when building vanilla CMake code against the release.
set( ATLAS_PLATFORM @ATLAS_PLATFORM@ )

# Packages provided by the project:
set( @CMAKE_PROJECT_NAME@_PACKAGE_NAMES @ATLAS_EXPORTED_PACKAGES@ )

# Include the base projects, in the order in which they were given to
# the atlas_projects call.
set( _baseProjects@CMAKE_PROJECT_NAME@
   ${@CMAKE_PROJECT_NAME@_BASE_PROJECTS} )
while( _baseProjects@CMAKE_PROJECT_NAME@ )
   # Extract the release name and version, and then remove these entries
   # from the list:
   list( GET _baseProjects@CMAKE_PROJECT_NAME@ 0
      _base_project@CMAKE_PROJECT_NAME@ )
   list( GET _baseProjects@CMAKE_PROJECT_NAME@ 1
      _base_version@CMAKE_PROJECT_NAME@ )
   list( REMOVE_AT _baseProjects@CMAKE_PROJECT_NAME@ 0 1 )
   # Find the base release:
   if( @CMAKE_PROJECT_NAME@_FIND_QUIETLY )
      find_package( ${_base_project@CMAKE_PROJECT_NAME@}
         ${_base_version@CMAKE_PROJECT_NAME@} EXACT QUIET )
   else()
      find_package( ${_base_project@CMAKE_PROJECT_NAME@}
         ${_base_version@CMAKE_PROJECT_NAME@} EXACT )
   endif()
endwhile()
unset( _baseProjects@CMAKE_PROJECT_NAME@ )
unset( _base_project@CMAKE_PROJECT_NAME@ )
unset( _base_version@CMAKE_PROJECT_NAME@ )

# Make CMake find the release's installed modules. Append the module
# library instead of prepending it. To allow the user to override the
# modules packaged with the release.
get_filename_component( @CMAKE_PROJECT_NAME@_MODULE_DIR
   "${_thisdir@CMAKE_PROJECT_NAME@}/modules" ABSOLUTE )
if( ATLAS_DONT_PREPEND_PROJECT_MODULES )
   list( APPEND CMAKE_MODULE_PATH ${@CMAKE_PROJECT_NAME@_MODULE_DIR} )
else()
   list( INSERT CMAKE_MODULE_PATH 0 ${@CMAKE_PROJECT_NAME@_MODULE_DIR} )
endif()
list( REMOVE_DUPLICATES CMAKE_MODULE_PATH )

# Pull in the ATLAS code:
include( AtlasFunctions )
include( AthenaFunctions )

# Include the project-specific pre-include file, if it exists:
if( EXISTS ${_thisdir@CMAKE_PROJECT_NAME@}/PreConfig.cmake )
   include( ${_thisdir@CMAKE_PROJECT_NAME@}/PreConfig.cmake )
endif()

# Include the file listing all the imported targets and options:
if( EXISTS
   ${_thisdir@CMAKE_PROJECT_NAME@}/@CMAKE_PROJECT_NAME@Config-targets.cmake )
   include(
      ${_thisdir@CMAKE_PROJECT_NAME@}/@CMAKE_PROJECT_NAME@Config-targets.cmake )
endif()

# Check what build mode the release was built with. And set CMAKE_BUILD_TYPE
# to that value by default. While there should only be one build mode in
# a given install area, provide an explicit preference order to the different
# build modes:
foreach( _type Debug RelWithDebInfo Release MinSizeRel Default )
   string( TOLOWER "${_type}" _typeLower )
   set( _fileName "${_thisdir@CMAKE_PROJECT_NAME@}/" )
   set( _fileName "${_fileName}@CMAKE_PROJECT_NAME@Config-targets" )
   set( _fileName "${_fileName}-${_typeLower}.cmake" )
   if( EXISTS "${_fileName}" AND NOT CMAKE_BUILD_TYPE )
      set( CMAKE_BUILD_TYPE "${_type}"
         CACHE STRING "Build mode for the release" FORCE )
   endif()
   unset( _fileName )
   unset( _typeLower )
endforeach()

# Tell the user how many packages were found:
if( NOT @CMAKE_PROJECT_NAME@_FIND_QUIETLY )
   list( LENGTH @CMAKE_PROJECT_NAME@_PACKAGE_NAMES _nPackages )
   message( STATUS "Package(s) in @CMAKE_PROJECT_NAME@: ${_nPackages}" )
   unset( _nPackages )
endif()

# Set the CMake code into "release mode" for the next bit:
set( ATLAS_RELEASE_MODE ON )
set( ATLAS_RELEASE_NAME @CMAKE_PROJECT_NAME@ )

# If recompilation is requested:
if( @CMAKE_PROJECT_NAME@_FIND_COMPONENTS )

   # Put the packages either into recompilation or simple inclusion mode:
   if( ${@CMAKE_PROJECT_NAME@_FIND_COMPONENTS} STREQUAL "RECOMPILE" OR
         ${@CMAKE_PROJECT_NAME@_FIND_COMPONENTS} STREQUAL "RECOMPILE_DRYRUN" OR
         ${@CMAKE_PROJECT_NAME@_FIND_COMPONENTS} STREQUAL "RECOMPILE_SETUP"  )
      set( ATLAS_RELEASE_RECOMPILE ON )
   elseif( ${@CMAKE_PROJECT_NAME@_FIND_COMPONENTS} STREQUAL "INCLUDE" )
      set( ATLAS_RELEASE_RECOMPILE OFF )
   else()
      message( WARNING "Please use either 'RECOMPILE' or 'INCLUDE' as\n"
         "the COMPONENTS argument of the find_package(...)\n"
         "call. Assuming 'INCLUDE' mode for now." )
      set( ATLAS_RELEASE_RECOMPILE OFF )
   endif()

   # Include the packages. The user must not call find_package
   # multiple times with COMPONENTS options. So no protection is added
   # here...

   # Find all the packages that need to be recompiled. This includes
   # packages that only indirectly depend on a package in the work area.
   # (Through another package in the middle.) To find all the dependencies,
   # the CMakeLists.txt files of the release are scanned multiple times.
   # Until no new package is found that should be recompiled.
   if( ATLAS_RELEASE_RECOMPILE )

      # Find the packages in the source directory:
      set( @CMAKE_PROJECT_NAME@_packageDirs )
      file( GLOB_RECURSE cmakelist_files RELATIVE ${CMAKE_SOURCE_DIR}
         "${_releasedir@CMAKE_PROJECT_NAME@}/src/*/CMakeLists.txt" )
      foreach( file ${cmakelist_files} )
         get_filename_component( package ${file} DIRECTORY )
         list( APPEND @CMAKE_PROJECT_NAME@_packageDirs ${package} )
      endforeach()
      unset( cmakelist_files )
      unset( package )

      # Only do the scanning if we've not finished with it, starting from
      # a higher level project, already:
      if( NOT ${@CMAKE_PROJECT_NAME@_FIND_COMPONENTS} STREQUAL
            "RECOMPILE_SETUP" )

         # Decide which packages need to be recompiled:
         message( STATUS "Scanning project @CMAKE_PROJECT_NAME@ for "
            "packages to recompile..." )
         set( ATLAS_RELEASE_RECOMPILE_DRYRUN ON )
         set( _keepScanning@CMAKE_PROJECT_NAME@ ON )
         if( NOT DEFINED _iteration )
            set( _iteration 0 )
         endif()
         while( _keepScanning@CMAKE_PROJECT_NAME@ )
            # Because of the recursiveness of the next bit, make sure that the
            # project name is still set correctly:
            set( ATLAS_RELEASE_NAME @CMAKE_PROJECT_NAME@ )
            # A helper variable:
            math( EXPR _iteration "${_iteration} + 1" )
            # Check how many packages are in the recompilation list right now:
            get_property( _recompilePackages@CMAKE_PROJECT_NAME@ GLOBAL PROPERTY
               ATLAS_PACKAGES_TO_RECOMPILE )
            list( LENGTH _recompilePackages@CMAKE_PROJECT_NAME@
               _nBefore@CMAKE_PROJECT_NAME@ )
            # Dryrun all the packages in this project:
            foreach( pkgDir ${@CMAKE_PROJECT_NAME@_packageDirs} )
               get_filename_component( pkgName ${pkgDir} NAME )
               # This variable is only defined in this weird way to be able to
               # stay within an 80 column width with the source.
               set( _buildDir "${CMAKE_BINARY_DIR}/@CMAKE_PROJECT_NAME@" )
               set( _buildDir "${_buildDir}/dryrun${_iteration}/${pkgName}" )
               add_subdirectory( ${pkgDir} ${_buildDir} )
            endforeach()
            # Now dryrun all the packages in the base project(s):
            set( _baseProjects@CMAKE_PROJECT_NAME@
               ${@CMAKE_PROJECT_NAME@_BASE_PROJECTS} )
            while( _baseProjects@CMAKE_PROJECT_NAME@ )
               list( GET _baseProjects@CMAKE_PROJECT_NAME@ 0
                  _base_project@CMAKE_PROJECT_NAME@ )
               list( GET _baseProjects@CMAKE_PROJECT_NAME@ 1
                  _base_version@CMAKE_PROJECT_NAME@ )
               list( REMOVE_AT _baseProjects@CMAKE_PROJECT_NAME@ 0 1 )
               if( @CMAKE_PROJECT_NAME@_FIND_QUIETLY )
                  find_package( ${_base_project@CMAKE_PROJECT_NAME@}
                     ${_base_version@CMAKE_PROJECT_NAME@} EXACT QUIET
                     COMPONENTS RECOMPILE_DRYRUN )
               else()
                  find_package( ${_base_project@CMAKE_PROJECT_NAME@}
                     ${_base_version@CMAKE_PROJECT_NAME@} EXACT
                     COMPONENTS RECOMPILE_DRYRUN )
               endif()
            endwhile()
            unset( _baseProjects@CMAKE_PROJECT_NAME@ )
            unset( _base_project@CMAKE_PROJECT_NAME@ )
            unset( _base_version@CMAKE_PROJECT_NAME@ )
            # Check if the dry running needs to continue or not:
            get_property( _recompilePackages@CMAKE_PROJECT_NAME@ GLOBAL PROPERTY
               ATLAS_PACKAGES_TO_RECOMPILE )
            list( LENGTH _recompilePackages@CMAKE_PROJECT_NAME@
               _nAfter@CMAKE_PROJECT_NAME@ )
            if( _nBefore@CMAKE_PROJECT_NAME@ EQUAL _nAfter@CMAKE_PROJECT_NAME@ )
               set( _keepScanning@CMAKE_PROJECT_NAME@ OFF )
            endif()
         endwhile()
         # This should be shown even in quiet mode:
         message( STATUS "Found ${_nAfter@CMAKE_PROJECT_NAME@} packages "
            "needing recompilation, in ${_iteration} iteration(s) "
            "(@CMAKE_PROJECT_NAME@)")
         # Clean up:
         unset( _keepScanning@CMAKE_PROJECT_NAME@ )
         unset( _nBefore@CMAKE_PROJECT_NAME@ )
         unset( _nAfter@CMAKE_PROJECT_NAME@ )
         unset( _recompilePackages@CMAKE_PROJECT_NAME@ )
      endif()

      # If we only had to do a dryrun, then exit now:
      if( ${@CMAKE_PROJECT_NAME@_FIND_COMPONENTS} STREQUAL "RECOMPILE_DRYRUN" )
         return()
      endif()

      # Now we're out of the dryrun phase:
      set( ATLAS_RELEASE_RECOMPILE_DRYRUN OFF )

      # Some further cleanup:
      unset( _iteration )

      # Now include all the packages. They should know which one of them
      # need recompilation.
      message( STATUS "Setting up the packages from project "
         "@CMAKE_PROJECT_NAME@ - @CMAKE_PROJECT_VERSION@..." )
      set( ATLAS_RELEASE_NAME @CMAKE_PROJECT_NAME@ )
      foreach( pkgDir ${@CMAKE_PROJECT_NAME@_packageDirs} )
         get_filename_component( pkgName ${pkgDir} NAME )
         add_subdirectory( ${pkgDir}
            ${CMAKE_BINARY_DIR}/@CMAKE_PROJECT_NAME@/${pkgName} )
      endforeach()

   else()

      # Tell the user what's happening:
      message( STATUS "Including the packages from project "
         "@CMAKE_PROJECT_NAME@ - @CMAKE_PROJECT_VERSION@..." )

      # Targets to pull into the current project:
      set( @CMAKE_PROJECT_NAME@_TARGET_NAMES @ATLAS_EXPORTED_TARGETS@ )

      # Loop over the targets that this project has:
      foreach( _target ${@CMAKE_PROJECT_NAME@_TARGET_NAMES} )
         # If the target exists already, then don't do aything else:
         if( TARGET ${_target} )
            continue()
         endif()
         # Check whether the target in question is known in this release:
         if( NOT TARGET @CMAKE_PROJECT_NAME@::${_target} )
            message( SEND_ERROR
               "Target with name @CMAKE_PROJECT_NAME@::${_target} not found" )
            continue()
         endif()
         # Get the type of this target:
         get_property( _type TARGET @CMAKE_PROJECT_NAME@::${_target}
            PROPERTY TYPE )
         # And now create a copy of this target in the right way:
         if( "${_type}" STREQUAL "SHARED_LIBRARY" OR
               "${_type}" STREQUAL "MODULE_LIBRARY" OR
               "${_type}" STREQUAL "INTERFACE_LIBRARY" )
            # First, create the right type of library:
            if( "${_type}" STREQUAL "SHARED_LIBRARY" )
               # Create a shared library:
               add_library( ${_target} SHARED IMPORTED GLOBAL )
            elseif( "${_type}" STREQUAL "MODULE_LIBRARY" )
               # Create a module library:
               add_library( ${_target} MODULE IMPORTED GLOBAL )
            elseif( "${_type}" STREQUAL "INTERFACE_LIBRARY" )
               # Create an interface library:
               add_library( ${_target} INTERFACE IMPORTED GLOBAL )
            endif()
            # Copy its dependencies:
            get_property( _libSet TARGET @CMAKE_PROJECT_NAME@::${_target}
               PROPERTY INTERFACE_LINK_LIBRARIES SET )
            if( _libSet )
               get_target_property( _libraries @CMAKE_PROJECT_NAME@::${_target}
                  INTERFACE_LINK_LIBRARIES )
               string( REPLACE "@CMAKE_PROJECT_NAME@::" "" _libraries
                  "${_libraries}" )
               set_target_properties( ${_target} PROPERTIES
                  INTERFACE_LINK_LIBRARIES "${_libraries}" )
               unset( _libraries )
            endif()
            unset( _libSet )
            get_property( _incSet TARGET @CMAKE_PROJECT_NAME@::${_target}
               PROPERTY INTERFACE_SYSTEM_INCLUDE_DIRECTORIES SET )
            if( _incSet )
               get_target_property( _includes @CMAKE_PROJECT_NAME@::${_target}
                  INTERFACE_SYSTEM_INCLUDE_DIRECTORIES )
               string( REPLACE "@CMAKE_PROJECT_NAME@::" "" _includes
                  "${_includes}" )
               set_target_properties( ${_target} PROPERTIES
                  INTERFACE_SYSTEM_INCLUDE_DIRECTORIES "${_includes}" )
               unset( _includes )
            endif()
            get_property( _incSet TARGET @CMAKE_PROJECT_NAME@::${_target}
               PROPERTY INTERFACE_INCLUDE_DIRECTORIES SET )
            if( _incSet )
               get_target_property( _includes @CMAKE_PROJECT_NAME@::${_target}
                  INTERFACE_INCLUDE_DIRECTORIES )
               string( REPLACE "@CMAKE_PROJECT_NAME@::" "" _includes
                  "${_includes}" )
               set_target_properties( ${_target} PROPERTIES
                  INTERFACE_INCLUDE_DIRECTORIES "${_includes}" )
               unset( _includes )
            endif()
            unset( _incSet )
            get_property( _defSet TARGET @CMAKE_PROJECT_NAME@::${_target}
               PROPERTY INTERFACE_COMPILE_DEFINITIONS SET )
            if( _defSet )
               get_target_property( _definitions
                  @CMAKE_PROJECT_NAME@::${_target}
                  INTERFACE_COMPILE_DEFINITIONS )
               set_target_properties( ${_target} PROPERTIES
                  INTERFACE_COMPILE_DEFINITIONS "${_definitions}" )
               unset( _definitions )
            endif()
            unset( _defSet )
            # For physical libraries, copy their location as well:
            if( "${_type}" STREQUAL "SHARED_LIBRARY" OR
                  "${_type}" STREQUAL "MODULE_LIBRARY" )
               # Get the configurations that it is available for:
               get_target_property( _builds @CMAKE_PROJECT_NAME@::${_target}
                  IMPORTED_CONFIGURATIONS )
               set_target_properties( ${_target} PROPERTIES
                  IMPORTED_CONFIGURATIONS ${_builds} )
               foreach( _build ${_builds} )
                  # Copy the properties:
                  get_target_property( _location
                     @CMAKE_PROJECT_NAME@::${_target}
                     IMPORTED_LOCATION_${_build} )
                  get_target_property( _soname
                     @CMAKE_PROJECT_NAME@::${_target}
                     IMPORTED_SONAME_${_build} )
                  set_target_properties( ${_target} PROPERTIES
                     IMPORTED_LOCATION_${_build} "${_location}"
                     IMPORTED_SONAME_${_build} "${_soname}" )
                  unset( _location )
                  unset( _soname )
                  get_property( _depLibSet
                     TARGET @CMAKE_PROJECT_NAME@::${_target}
                     PROPERTY IMPORTED_LINK_DEPENDENT_LIBRARIES_${_build} SET )
                  if( _depLibSet )
                     get_target_property( _depLib
                        @CMAKE_PROJECT_NAME@::${_target}
                        IMPORTED_LINK_DEPENDENT_LIBRARIES_${_build} )
                     string( REPLACE "@CMAKE_PROJECT_NAME@::" ""
                        _depLib "${_depLib}" )
                     set_target_properties( ${_target} PROPERTIES
                        IMPORTED_LINK_DEPENDENT_LIBRARIES_${_build}
                        "${_depLib}" )
                     unset( _depLib )
                  endif()
                  unset( _depLibSet )
               endforeach()
               unset( _builds )
            endif()
         elseif( "${_type}" STREQUAL "EXECUTABLE" )
            # Set up the imported executable:
            add_executable( ${_target} IMPORTED GLOBAL )
            # Copy its location for all available configurations:
            get_target_property( _builds @CMAKE_PROJECT_NAME@::${_target}
               IMPORTED_CONFIGURATIONS )
            set_target_properties( ${_target} PROPERTIES
               IMPORTED_CONFIGURATIONS "${_builds}" )
            foreach( _build ${_builds} )
               # Copy the properties:
               get_target_property( _location @CMAKE_PROJECT_NAME@::${_target}
                  IMPORTED_LOCATION_${_build} )
               set_target_properties( ${_target} PROPERTIES
                  IMPORTED_LOCATION_${_build} "${_location}" )
               unset( _location )
            endforeach()
            unset( _builds )
         else()
            message( SEND_ERROR "Unknown target type (${_type}) encountered" )
         endif()
         unset( _type )
      endforeach()

   endif()

   # Include the base projects, in reverse order. So that the components from
   # the end of the list would get precedence over the components from the
   # front.
   set( _baseProjects@CMAKE_PROJECT_NAME@
      ${@CMAKE_PROJECT_NAME@_BASE_PROJECTS} )
   while( _baseProjects@CMAKE_PROJECT_NAME@ )
      # Get the last project from the list:
      list( LENGTH _baseProjects@CMAKE_PROJECT_NAME@
         _length@CMAKE_PROJECT_NAME@ )
      math( EXPR _projNameIdx@CMAKE_PROJECT_NAME@
         "${_length@CMAKE_PROJECT_NAME@} - 2" )
      math( EXPR _projVersIdx@CMAKE_PROJECT_NAME@
         "${_length@CMAKE_PROJECT_NAME@} - 1" )
      list( GET _baseProjects@CMAKE_PROJECT_NAME@
         ${_projNameIdx@CMAKE_PROJECT_NAME@}
         _base_project@CMAKE_PROJECT_NAME@ )
      list( GET _baseProjects@CMAKE_PROJECT_NAME@
         ${_projVersIdx@CMAKE_PROJECT_NAME@}
         _base_version@CMAKE_PROJECT_NAME@ )
      list( REMOVE_AT _baseProjects@CMAKE_PROJECT_NAME@
         ${_projNameIdx@CMAKE_PROJECT_NAME@}
         ${_projVersIdx@CMAKE_PROJECT_NAME@} )
      # Find the base release:
      if( ${@CMAKE_PROJECT_NAME@_FIND_COMPONENTS} STREQUAL "INCLUDE" )
         find_package( ${_base_project@CMAKE_PROJECT_NAME@}
            ${_base_version@CMAKE_PROJECT_NAME@}
            COMPONENTS INCLUDE
            EXACT QUIET )
      elseif( ${@CMAKE_PROJECT_NAME@_FIND_COMPONENTS} STREQUAL "RECOMPILE" OR
            ${@CMAKE_PROJECT_NAME@_FIND_COMPONENTS} STREQUAL "RECOMPILE_SETUP" )
         # By this time all the dry running has been done. So let's just
         # set up the packages for recompilation already.
         find_package( ${_base_project@CMAKE_PROJECT_NAME@}
            ${_base_version@CMAKE_PROJECT_NAME@}
            COMPONENTS RECOMPILE_SETUP
            EXACT QUIET )
      endif()
   endwhile()
   unset( _baseProjects@CMAKE_PROJECT_NAME@ )
   unset( _projNameIdx@CMAKE_PROJECT_NAME@ )
   unset( _projVersIdx@CMAKE_PROJECT_NAME@ )
   unset( _base_project@CMAKE_PROJECT_NAME@ )
   unset( _base_version@CMAKE_PROJECT_NAME@ )
   unset( _length@CMAKE_PROJECT_NAME@ )

endif()

# Take CMake out of "release mode":
set( ATLAS_RELEASE_MODE OFF )

# Only do this if necessary:
if( NOT ATLAS_DONT_PREPEND_PROJECT_MODULES )
   # Make sure that after all of this, we still have this release's module
   # directory at the front of the list:
   list( INSERT CMAKE_MODULE_PATH 0 ${@CMAKE_PROJECT_NAME@_MODULE_DIR} )
   list( REMOVE_DUPLICATES CMAKE_MODULE_PATH )
endif()

# Include the project-specific post-include file, if it exists:
if( EXISTS ${_thisdir@CMAKE_PROJECT_NAME@}/PostConfig.cmake )
   include( ${_thisdir@CMAKE_PROJECT_NAME@}/PostConfig.cmake )
endif()

# Remove the unwanted variables:
unset( _thisdir@CMAKE_PROJECT_NAME@ )
unset( _releasedir@CMAKE_PROJECT_NAME@ )
