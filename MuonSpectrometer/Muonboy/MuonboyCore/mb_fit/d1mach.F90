!
!> DOUBLE-PRECISION MACHINE CONSTANTS
!> D1MACH( 1) = B**(EMIN-1), THE SMALLEST POSITIVE MAGNITUDE.
!> D1MACH( 2) = B**EMAX*(1 - B**(-T)), THE LARGEST MAGNITUDE.
!> D1MACH( 3) = B**(-T), THE SMALLEST RELATIVE SPACING.
!> D1MACH( 4) = B**(1-T), THE LARGEST RELATIVE SPACING.
!> D1MACH( 5) = LOG10(B)
!
 REAL(8) FUNCTION D1MACH(I)
 INTEGER,INTENT(IN):: I
 REAL(8), DIMENSION(5), SAVE :: X
 LOGICAL, SAVE :: ran = .false.
   IF( .NOT. ran )THEN
     X(1) = TINY(X(1))
     X(2) = HUGE(X(2))
     X(3) = EPSILON(X(3))/RADIX(X(3))
     X(4) = EPSILON(X(4))
     X(5) = LOG10(REAL(RADIX(X(5)),KIND=8))
     ran = .true.
   END IF 

   IF( I < 1 .OR. I > 5 ) THEN
     WRITE(*,*) 'D1MACH(I): I =',I,' is out of bounds.'
     STOP
   END IF
   D1MACH = X(I)
 END FUNCTION D1MACH
!
